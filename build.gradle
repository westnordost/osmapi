apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'signing'

group = "de.westnordost"
archivesBaseName = "osmapi"
version = 1.6

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    compile 'oauth.signpost:signpost-core:1.2.1.2'
    compile 'xmlpull:xmlpull:1.1.3.1'
    runtime 'net.sf.kxml:kxml2:2.3.0'
    testCompile 'junit:junit:3.8.1'
}

repositories {
    mavenCentral()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'osmapi'
        packaging 'jar'
        description 'Java implementation of the OSM API 0.6'
        url 'https://github.com/westnordost/osmapi'

        scm {
          connection 'https://github.com/westnordost/osmapi.git'
          developerConnection 'https://github.com/westnordost/osmapi.git'
          url 'https://github.com/westnordost/osmapi'
        }

        licenses {
          license {
            name 'GNU Lesser General Public License, Version 3.0'
            url 'http://www.gnu.org/licenses/lgpl-3.0.html'
          }
        }

        developers {
          developer {
            id 'westnordost'
            name 'Tobias Zwick'
            email 'osm@westnordost.de'
          }
        }
      }
    }
  }
}